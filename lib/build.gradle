apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'maven'

def versionNameConst = '1.0.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName versionNameConst
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def buildBranchKey = 'build.branch'
def deploySnapshotBranch = 'dev'
def deployReleaseBranch = 'master'

uploadArchives {
    repositories {
        if (project.properties['teamcity']?.containsKey(buildBranchKey)) {
            def buildBranch = project.teamcity[buildBranchKey]
            def version = versionNameConst
            if (buildBranch == deploySnapshotBranch) version += "-SNAPSHOT"
            if (buildBranch == deploySnapshotBranch || buildBranch == deployReleaseBranch) {
                mavenDeployer {
                    pom.groupId = 'com.yandex.yaloginsdk'
                    pom.version = version

                    ext.auth = {
                        authentication(userName: 'yandexmobiledeploy', password: 'yandexmobiledeploy')
                    }
                    repository(url: 'http://artifactory.yandex.net/artifactory/yandex_mobile_releases/', auth)
                    snapshotRepository(url: 'http://artifactory.yandex.net/artifactory/yandex_mobile_snapshots/', auth)
                }
            } else {
                logger.lifecycle "feature branch - skipping uploadArchives"
            }
        } else {
            logger.lifecycle "local build - skipping uploadArchives"
        }
    }
}

dependencies {
    //noinspection GradleCompatible using lowest version
    compile 'com.android.support:appcompat-v7:22.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.2'
    testCompile 'org.robolectric:shadows-support-v4:3.1.2'
    testCompile 'org.assertj:assertj-core:2.4.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

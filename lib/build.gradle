apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

group = "com.yandex.android"
ext.artifactId = "authsdk"

apply from: '../publishing.gradle'
apply from: 'deeplink_config.gradle'

def getBuildNumber() {
    (System.getenv('BUILD_NUMBER') ?: Integer.MAX_VALUE) as int
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
        versionCode getBuildNumber()
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [YANDEX_OAUTH_HOST: "oauth.yandex.ru"]

        def propertiesFile = rootProject.file('gradle.properties')
        def properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))

        def addClientId = {
            List clientIds = new ArrayList<String>()
            Integer clientIndex = 0

            HashMap indexedClientIds = new HashMap<String, Integer>()

            while (clientIndex != -1) {
                String currentClientId

                if (clientIndex == 0) {
                    currentClientId = properties['YANDEX_CLIENT_ID']
                } else {
                    currentClientId = properties['YANDEX_CLIENT_ID_' + clientIndex]
                }
                println "currentClindId $currentClientId"
                if (currentClientId != null) {
                    clientIds.add(currentClientId)
                    indexedClientIds.put(currentClientId, clientIndex)
                    clientIndex++
                } else {
                    clientIndex = -1
                }
            }
            manifestPlaceholders += [deepLinks_manifestPlaceholder: generateData(clientIds, manifestPlaceholders.get("YANDEX_OAUTH_HOST"))]
            manifestPlaceholders += [deepLinks_custom_manifestPlaceholder: generateCustomData(clientIds)]
            manifestPlaceholders += [metaData_manifestPlaceholder: generateMeta(indexedClientIds)]
        }

        addClientId()

        def addConstant = { constantName, constantValue ->
            manifestPlaceholders += [(constantName): constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("ACTION_YA_SDK_LOGIN", "com.yandex.auth.action.YA_SDK_LOGIN")
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    unitTestVariants.all {
        it.mergedFlavor.manifestPlaceholders += [
            YANDEX_CLIENT_ID: "fcdddf83a97843ae80815c1c9247015b",
        ]
    }
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'com.android.support:support-annotations:28.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.mockito:mockito-core:4.1.0'
}
